<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="FB_CsvPersistentEventStorage" Id="{f900b878-3549-4dc8-8bca-2bfd667b8d82}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_CsvPersistentEventStorage IMPLEMENTS I_PersistentEventStorage
VAR
	fbFileOpen : FB_FileOpen := (sPathName := 'c:\events.log', nMode := FOPEN_MODEAPPEND OR FOPEN_MODEBINARY);
	fbFilePuts : FB_FilePuts;
	fbFileClose : FB_FileClose;
	eWriteState : (FILE_OPEN_TRIGGER, FILE_OPEN, WAIT_FOR_EVENT, WRITE_EVENT_TRIGGER, WRITE_EVENT, FILE_CLOSE_TRIGGER, FILE_CLOSE, ERROR);
	stEventWrittenToFile : ST_Event;
	
	CsvString : T_MaxString;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[CASE eWriteState OF
	FILE_OPEN_TRIGGER :
		fbFileOpen(bExecute := TRUE);
		eWriteState := FILE_OPEN;
	FILE_OPEN :
		fbFileOpen(bExecute := FALSE);
		IF fbFileOpen.bError THEN
			eWriteState := ERROR;
		ELSIF NOT fbFileOpen.bBusy AND fbFileOpen.hFile <> 0 THEN
			eWriteState := WAIT_FOR_EVENT;
		END_IF
	WAIT_FOR_EVENT :
		// Do nothing
	WRITE_EVENT_TRIGGER :
		CsvString := CreateCsvString(stEvents := stEventWrittenToFile);
		fbFilePuts(bExecute := TRUE,
			sLine := CsvString,
			hFile := fbFileOpen.hFile);
		eWriteState := WRITE_EVENT;
	WRITE_EVENT :
		fbFilePuts(bExecute := FALSE);
		IF fbFilePuts.bError THEN
			eWriteState := ERROR;
		ELSIF NOT fbFilePuts.bBusy THEN
			eWriteState := FILE_CLOSE_TRIGGER;
		END_IF
	FILE_CLOSE_TRIGGER :
		fbFileClose(bExecute := TRUE,
			hFile := fbFileOpen.hFile);
		eWriteState := FILE_CLOSE;
	FILE_CLOSE :
		fbFileClose(bExecute := FALSE);
		IF fbFileClose.bError THEN
			eWriteState := ERROR;
		ELSIF NOT fbFileClose.bBusy THEN
			eWriteState := WAIT_FOR_EVENT;
		END_IF
	ERROR :
		// Do nothing
END_CASE
]]></ST>
    </Implementation>
    <Method Name="CreateCsvString" Id="{bd869ead-9bc7-4210-a181-ce1f64a4fa8f}">
      <Declaration><![CDATA[METHOD PRIVATE CreateCsvString : T_MaxString
VAR_INPUT
	stEvents : REFERENCE TO ST_Event;
END_VAR
VAR
	TempString : T_MaxString;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[TempString := TO_STRING(stEvents.eEventType);
tempstring := CONCAT(STR1 := TempString, str2 := ', ');
tempstring := CONCAT(STR1 := TempString, str2 := TO_STRING(stEvents.eEventSeverity));
tempstring := CONCAT(STR1 := TempString, str2 := ', ');
tempstring := CONCAT(STR1 := TempString, str2 := TO_STRING(stEvents.nEventIdentity));
tempstring := CONCAT(STR1 := TempString, str2 := ', ');
tempstring := CONCAT(STR1 := TempString, str2 := stEvents.sEventText);
tempstring := CONCAT(STR1 := TempString, str2 := ', ');
tempstring := CONCAT(STR1 := TempString, str2 := TO_STRING(stEvents.dtTimestamp));
tempstring := CONCAT(STR1 := TempString, str2 := '$n');

CreateCsvString := tempstring;]]></ST>
      </Implementation>
    </Method>
    <Method Name="StoreEvent" Id="{b1a5ea6e-4741-449b-903c-f45a8fe8c40f}">
      <Declaration><![CDATA[METHOD StoreEvent
VAR_INPUT
	stEvent	: ST_Event;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF eWriteState = WAIT_FOR_EVENT THEN
	eWriteState := WRITE_EVENT_TRIGGER;
	stEventWrittenToFile := stEvent;
END_IF]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_CsvPersistentEventStorage">
      <LineId Id="18" Count="0" />
      <LineId Id="31" Count="1" />
      <LineId Id="41" Count="0" />
      <LineId Id="19" Count="0" />
      <LineId Id="35" Count="2" />
      <LineId Id="43" Count="0" />
      <LineId Id="45" Count="0" />
      <LineId Id="38" Count="0" />
      <LineId Id="20" Count="0" />
      <LineId Id="25" Count="0" />
      <LineId Id="48" Count="0" />
      <LineId Id="52" Count="0" />
      <LineId Id="49" Count="0" />
      <LineId Id="53" Count="0" />
      <LineId Id="61" Count="0" />
      <LineId Id="54" Count="0" />
      <LineId Id="21" Count="0" />
      <LineId Id="55" Count="5" />
      <LineId Id="22" Count="0" />
      <LineId Id="27" Count="0" />
      <LineId Id="63" Count="0" />
      <LineId Id="67" Count="0" />
      <LineId Id="64" Count="0" />
      <LineId Id="68" Count="5" />
      <LineId Id="33" Count="0" />
      <LineId Id="46" Count="0" />
      <LineId Id="23" Count="0" />
      <LineId Id="9" Count="0" />
    </LineIds>
    <LineIds Name="FB_CsvPersistentEventStorage.CreateCsvString">
      <LineId Id="5" Count="0" />
      <LineId Id="10" Count="0" />
      <LineId Id="12" Count="8" />
      <LineId Id="11" Count="0" />
    </LineIds>
    <LineIds Name="FB_CsvPersistentEventStorage.StoreEvent">
      <LineId Id="7" Count="0" />
      <LineId Id="10" Count="1" />
      <LineId Id="9" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>